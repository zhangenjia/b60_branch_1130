plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdk
    buildToolsVersion rootProject.ext.android.buildTools
    defaultConfig {
        applicationId "com.adayo.app.music"
        minSdkVersion rootProject.ext.android.minSdk
        targetSdkVersion rootProject.ext.android.targetSdk
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }
    signingConfigs {
        release {
            storeFile file(rootProject.ext.android.storeFile)
            storePassword rootProject.ext.android.storePassword
            keyAlias rootProject.ext.android.keyAlias
            keyPassword rootProject.ext.android.keyPassword
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding = true
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "AdayoMusic.apk"
        }
    }//配置生成产物的文件名
}

dependencies {
    /**
     * Module Lib
     */
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    /**
     * Project Lib
     */
    //noinspection GradlePath
    implementation project(path: ':music-common-res')
    implementation project(path: ':music_bt')

    /**
     * Android Lib
     */
    implementation 'com.google.android.material:material:' + rootProject.ext.dependencies['material']
    implementation 'androidx.appcompat:appcompat:' + rootProject.ext.dependencies['appcompat']
    implementation 'androidx.recyclerview:recyclerview:' + rootProject.ext.dependencies['recyclerview']
    implementation 'androidx.constraintlayout:constraintlayout:' + rootProject.ext.dependencies['constraintlayout']
    implementation 'androidx.lifecycle:lifecycle-common-java8:' + rootProject.ext.dependencies['lifecycle']
    implementation 'androidx.lifecycle:lifecycle-service:' + rootProject.ext.dependencies['lifecycle']

    /**
     * CPY Maven Lib
     */
    compileOnly 'com.adayo.source:car:' + rootProject.ext.dependencies['car']
    implementation group: 'com.adayo.util', name: 'h-skin', version: rootProject.ext.dependencies['h-skin'], ext: 'aar'
    implementation 'com.adayo.util:h-skin-helper.hm6c17a:' + rootProject.ext.dependencies['h-skin-helper.hm6c17a']
    implementation 'com.adayo.proxy:media:' + rootProject.ext.dependencies['media']

    /**
     * Third Party Lib
     */
    implementation 'com.github.bumptech.glide:glide:' + rootProject.ext.dependencies['glide']
    annotationProcessor 'com.github.bumptech.glide:compiler:' + rootProject.ext.dependencies['glide']
    implementation 'com.github.zuilintan:lt-base:' + rootProject.ext.dependencies['lt-base']
    implementation 'com.kunminx.arch:unpeek-livedata:' + rootProject.ext.dependencies['unpeek-livedata']
    implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever-core:' + rootProject.ext.dependencies['FFmpegMediaMetadataRetriever']
    implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever-native-arm64-v8a:' + rootProject.ext.dependencies['FFmpegMediaMetadataRetriever']
}
